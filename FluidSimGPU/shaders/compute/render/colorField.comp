#version 430 core

layout (local_size_x = 1024) in; //working in 1D

//this struct has to have size equal to multiple of largest element,
//in this case that is vec2 with 8 bytes, the total of the struct is 32 bytes.
struct Particle {
	vec2 position;
	vec2 predictedPosition;
	vec2 velocity;
	float lamda;
	int properties;
};

//largest is vec4 with 16 bytes, total is 32
struct Properties { //particle properties
	float mass;
	float viscosity;
	float restDensity;
	bool diseased;
	vec4 color;
};

layout(std430, binding = 0) buffer ParticleBuffer {
	Particle particles[];
};

layout(std430, binding = 1) buffer GridCells {
	int cellStart[];
};

layout(std430, binding = 2) buffer PropertiesBuffer {
	Properties properties[6];
	int diseased[6];
	float densities[];
};

layout(std430, binding = 4) buffer ColorBuffer {
	float values[103576];
	vec4 colors[103576];
};

layout(std140, binding = 0) uniform SimulationValues {
	int numParticles;
	int numCells;
	int xCells;
	int yCells;
	int screenWidth;
	int screenHeight;
	int apPower;
	int cNumCells;
	int cxCells;
	int cyCells;
	int finalIteration;
	float maxVelocity;
	float particleRadius;
	float boundDamping;
	float kernelRadius;
	float kr2;
	float poly6;
	float spikyGrad;
	float relaxation;
	float apDenominator;
	float apConstant;
	float deltaTime;
	float gravity;
	float cResolution;
	float cResHalf;
	float cRadius;
	float cRadius2;
};

void main() {
	uint id = gl_GlobalInvocationID.x;
	if (id >= cNumCells) return;

	//color grid coordinates
	int cy = int(floor(id / cxCells));
	int cx = int(id) - cy * cxCells;

	//get position of this cell corner
	vec2 position = vec2(cx * cResolution, cy * cResolution);

	//get coordinates for particle grid
	int px = int(floor(position.x / kernelRadius));
	int py = int(floor(position.y / kernelRadius));

	vec4 rgba = vec4(0, 0, 0, 0);
	float value = 0;
	float speed = 0;
	int neighbours = 0;
	float totalDistance2 = 0;

	//search for neighbours
	for (int y = py - 1; y <= py + 1; y++) {
		for (int x = px - 1; x <= px + 1; x++) {
			if (y < 0 || x < 0 || y >= yCells || x >= xCells) continue; //out of bounds

			int cellID = y * xCells + x;
			for (int i = cellStart[cellID]; i < cellStart[cellID + 1]; i++) { //loop through all particles in cell
				Particle o = particles[i];

				vec2 difference = position - o.position;
				float dist2 = dot(difference, difference);

				if (dist2 < cRadius2) {
//					value += (particleRadius * particleRadius) / dist2; //metaball calculation
//					rgba += properties[o.properties].color;
//					speed += length(o.velocity);
//					neighbours++;

					value += (particleRadius * particleRadius) / dist2; //metaball calculation
					rgba += properties[o.properties].color;// / (dist2 / 50);
					totalDistance2 += dist2;
					neighbours++;
				}
			}
		}
	}
	if (neighbours == 0) {
		values[id] = 0;
		colors[id] = vec4(0, 0, 0, 0);
		return;
	}

	//take averages, maybe we can do something more clever here
//	values[id] = value;
//	colors[id] = (rgba / neighbours) + speed / 10000.0;
	values[id] = value;
	colors[id] = rgba / neighbours;
}